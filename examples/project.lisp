(in-package #:org.shirakumo.fraf.trial.examples)

(define-shader-entity project-player (collision-body listener)
  ((hit :initform (make-hit) :accessor hit)))

(define-handler (project-player tick :after) (dt)
  (let ((spd (* 3.0 dt))
        (target (node :c (scene +main+))))
    (cond ((retained :shift)
           (when (retained :a)
             (nq* (orientation project-player) (qfrom-angle +vy3+ (+ spd))))
           (when (retained :d)
             (nq* (orientation project-player) (qfrom-angle +vy3+ (- spd))))
           (when (retained :w)
             (nq* (orientation project-player) (qfrom-angle +vx3+ (+ spd))))
           (when (retained :s)
             (nq* (orientation project-player) (qfrom-angle +vx3+ (- spd))))
           (when (retained :c)
             (nq* (orientation project-player) (qfrom-angle +vz3+ (+ spd))))
           (when (retained :space)
             (nq* (orientation project-player) (qfrom-angle +vz3+ (- spd)))))
          (T
           (when (retained :a)
             (incf (vx (location project-player)) (- spd)))
           (when (retained :d)
             (incf (vx (location project-player)) (+ spd)))
           (when (retained :w)
             (incf (vz (location project-player)) (- spd)))
           (when (retained :s)
             (incf (vz (location project-player)) (+ spd)))
           (when (retained :c)
             (incf (vy (location project-player)) (- spd)))
           (when (retained :space)
             (incf (vy (location project-player)) (+ spd)))))
    (debug-clear)
    (t<- (local-transform target) (local-transform project-player))
    (let* ((direction (q* (orientation project-player) +vx3+))
           (hit (trial::snap-object-to-level target :direction direction)))
      (cond (hit
             (debug-line (location project-player) (location target) :color (vec 1 1 1 1))
             (debug-draw (local-transform target)))
            (T
             (debug-line (location project-player) (v+* (location project-player) direction 100) :color (vec 0 1 0 1)))))))

(defmethod (setf physics-primitive) :after ((primitive primitive) (body project-player))
  (setf (physics-primitive (node :c (scene +main+))) primitive))

(defmethod reset ((player project-player))
  (vsetf (location player) 0 0 2.5)
  (qsetf (orientation player) 0 0 0 1))

(define-example project
  :title "Shape Projection"
  :description "Testing workbench for projecting/snapping objects onto the surface of another."
  (enter (make-instance 'display-controller) scene)
  (enter (make-instance 'vertex-entity :vertex-array (// 'trial 'grid)) scene)
  (enter (make-instance 'collision-body :name :a :primitive (make-sphere)) scene)
  (enter (make-instance 'collision-body :name :c :primitive (make-sphere)) scene)
  (enter (make-instance 'project-player :name :b :primitive (make-sphere) :location (vec 0 0 +2.5)) scene)
  (enter (make-instance 'target-camera :location (vec3 0.0 8 9) :target (vec 0 0 0) :fov 50) scene)
  (observe! (hit-location (hit (node :b scene))) :title "Location")
  (observe! (hit-normal (hit (node :b scene))) :title "Normal")
  (enter (make-instance 'render-pass) scene))

(defmethod setup-ui ((scene project-scene) panel)
  (let ((layout (make-instance 'alloy:grid-layout :col-sizes '(T 120 200) :row-sizes '(30)))
        (focus (make-instance 'alloy:vertical-focus-list)))
    (flet ((shapes ()
             (list (make-sphere)
                   (make-box)
                   (make-cylinder)
                   (make-cone)
                   (make-pill)
                   (make-pill :radius-bottom 1.0 :radius-top 0.5)
                   (make-plane)
                   (make-half-space)
                   (make-triangle)
                   (coerce-object (make-sphere) 'convex-mesh))))
      (alloy:enter "Source Shape" layout :row 0 :col 1)
      (alloy:represent (physics-primitive (node :b scene)) 'alloy:combo-set
                       :value-set (shapes) :layout-parent layout :focus-parent focus)
      (alloy:enter "Target Shape" layout :row 1 :col 1)
      (alloy:represent (physics-primitive (node :a scene)) 'alloy:combo-set
                       :value-set (shapes) :layout-parent layout :focus-parent focus)
      (alloy:enter "Location" layout :row 2 :col 1)
      (alloy:represent (location (node :a scene)) T
                       :layout-parent layout :focus-parent focus)
      (alloy:enter "Orientation" layout :row 3 :col 1)
      (alloy:represent (orientation (node :a scene)) T
                       :layout-parent layout :focus-parent focus)
      (alloy:enter "Reset" layout :row 4 :col 1)
      (make-instance 'alloy:button* :value "..." :on-activate (lambda ()
                                                                (reset (node :a scene))
                                                                (reset (node :b scene)))
                                    :layout-parent layout :focus-parent focus)
      (alloy:finish-structure panel layout focus))))
